const float PI = 3.14159265359;

float D_GGX(float NoH, float roughness){
    float a = NoH * roughness;
    float k = roughness / (1.0 - NoH * NoH + a * a);
    return k * k * (1.0 / PI);
}

vec3 F_Schlick(float cosTheta, vec3 f0) {
    return f0 + (vec3(1.0) - f0) * pow(clamp(1.0 - cosTheta, 0.0, 1.0), 5.0);
}

float  V_SmithGGXCorrelated(float NoV, float NoL, float a) {
    float a2 = a * a;
    float GGXL = NoV * sqrt((-NoL * a2 + NoL) * NoL + a2);
    float GGXV = NoV * sqrt((-NoV * a2 + NoV) * NoV + a2);
    return 0.5 / (GGXV + GGXL);
}

float Fd_Lambert() {
    return 1.0 / PI;
}

vec3 BRDF(vec3 L, vec3 V, vec3 N, vec3 f0, float roughness, vec3 albedo, float metallic){
    vec3 H = normalize(V + L);
    float NoV = clamp(dot(N, V), 1e-5, 1.0);
    float NoL = clamp(dot(N, L), 1e-5, 1.0);
    float NoH = clamp(dot(N, H), 0.0, 1.0);
    float LoH = clamp(dot(L, H), 0.0, 1.0);

    float D = D_GGX(NoH, roughness);
    vec3 F = F_Schlick(LoH, f0);
    float Vis = V_SmithGGXCorrelated(NoV, NoL, roughness);
    vec3 specular = D * F * Vis;

    vec3 kD = (vec3(1.0) - F) * (1.0 - metallic);
    vec3 diffuse = kD * albedo * Fd_Lambert();
    return (diffuse + specular) * NoL;
}